#include <Servo.h>
#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <Keypad.h>

// Initialize LCD
LiquidCrystal_I2C lcd(0x27, 16, 2);

// Initialize Servo
Servo doorServo;

// Set the pin for the LED
const int ledRed = 12;  // LED for Access Denied
const int ledGreen = 11; // LED for Access Granted

// Set the correct password
String password = "1234"; 
String inputPassword = "";

// Keypad configuration
const byte ROWS = 4; // Four rows
const byte COLS = 4; // Four columns
char keys[ROWS][COLS] = {
  {'1', '2', '3', 'A'},
  {'4', '5', '6', 'B'},
  {'7', '8', '9', 'C'},
  {'*', '0', '#', 'D'}
};

byte rowPins[ROWS] = {9, 8, 7, 6}; // Connect keypad rows to pins
byte colPins[COLS] = {5, 4, 3, 2}; // Connect keypad columns to pins

Keypad keypad = Keypad(makeKeymap(keys), rowPins, colPins, ROWS, COLS);

void setup() {
  // Set up the servo motor
  doorServo.attach(10);  // Servo motor pin
  doorServo.write(0);     // Lock position (0 degrees)

  // Set up LEDs
  pinMode(ledRed, OUTPUT);
  pinMode(ledGreen, OUTPUT);
  digitalWrite(ledRed, LOW);
  digitalWrite(ledGreen, LOW);

  // Initialize LCD
  lcd.begin(16, 2);
  lcd.backlight();
  lcd.setCursor(0, 0);
  lcd.print("Enter Password:");
}

void loop() {
  char key = keypad.getKey();

  if (key) {
    if (key == '#') { // '#' key to submit password
      if (inputPassword == password) {
        accessGranted();
      } else {
        accessDenied();
      }
      inputPassword = "";  // Reset input
    } else if (key == '*') { // '*' key to clear input
      inputPassword = "";
      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print("Enter Password:");
    } else {  // Collect input
      inputPassword += key;
      lcd.setCursor(0, 1);
      lcd.print(inputPassword);
    }
  }
}

void accessGranted() {
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Access Granted!");
  digitalWrite(ledGreen, HIGH);
  digitalWrite(ledRed, LOW);
  doorServo.write(90); // Unlock position (90 degrees)
  delay(5000);  // Keep unlocked for 5 seconds
  doorServo.write(0);  // Return to lock position
  digitalWrite(ledGreen, LOW);
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Enter Password:");
}

void accessDenied() {
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Access Denied!");
  digitalWrite(ledRed, HIGH);
  digitalWrite(ledGreen, LOW);
  delay(3000);  // Denied message for 3 seconds
  digitalWrite(ledRed, LOW);
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Enter Password:");
}
